declare name "Agostino Di Scipio - AUDIBLE ECOSYSTEMICS n.2";
declare author "Luca Spanedda";
declare author "Dario Sanfilippo";
declare version "alpha";
declare description " 2022 version - Realised on composer's instructions
    of the year 2017 edited in Lâ€™Aquila, Italy";

// import faust standard library
import("stdfaust.lib");
// import audible ecosystemics tests library
import("aetest.lib");
// PERFORMANCE SYSTEM SampleRate
SampleRate = 44100;

//------- ------------- ----- -----------
//-- LIBRARY -------------------------------------------------------------------
//------- --------


//----------------------------------------------------------------- UTILITIES --
// limit function for library and system
limit(maxl,minl,x) = x : max(minl, min(maxl));
// see signal values 
inspect(i, lower, upper) = _ <: _ , 
    vbargraph("sig_%2i [style:numerical]", lower, upper) : attach;
    //process = (os.osc(.01) : inspect(1, .1, -1, 1));
diffDebug(x) = an.abs_envelope_tau(1, (x-x')) * (SampleRate/2);

//-------------------------------------------------------------------- DELAYS --
delayfb(delSec,fb,x) = loop ~ _ : mem
with{ 
    loop(z) = ( (z * fb + x) @(ba.sec2samp(delSec)-1) );
};

//---------------------------------------------------------------- SAMPLEREAD --
sampler(lengthSec, memChunk, ratio, x) = 
    it.frwtable(3, bufferLen, .0, writePtr, x, readPtr) * window
    with {
        memChunkLimited = max(0.010, min(1, memChunk));
        bufferLen = lengthSec * SampleRate;
        writePtr = ba.period(bufferLen);
        grainLen = max(1, ba.if(writePtr > memChunkLimited * bufferLen, 
            memChunkLimited * bufferLen, 1));
        readPtr = y
            letrec {
                'y = (ratio + y) % grainLen;
            };
        window = min(1, abs(((readPtr + grainLen / 2) % grainLen) - 
            grainLen / 2) / 200);
    };
// process = sampler(4, hslider("memChunkLimited", 0.05, 0, 1, .001), 
    // hslider("ratio", 10, .1, 10, .001), os.osc(100)) <: _, _;

//--------------------------------------------------------------- INTEGRATOR ---
integrator(seconds, x) = an.abs_envelope_tau(limit(1000,.001,seconds), x);

//----------------------------------------------------------------- LOCALMAX ---
localMax(seconds, x) = loop ~ si.bus(4) : _ , ! , ! , !
with {
    loop(yState, timerState, peakState, timeInSamplesState) = 
        y , timer , peak , timeInSamples
    with {
        timeInSamples = ba.if(reset + 1 - 1', seconds * 
            ma.SR, timeInSamplesState);
        reset = timerState >= (timeInSamplesState - 1);
        timer = ba.if(reset, 1, timerState + 1);
        peak = max(abs(x), peakState * (1.0 - reset));
        y = ba.if(reset, peak', yState);
    };
};
//process = os.osc(.1245) : localMax(hslider("windowlocalM",-1,-1,8,.001));
localmax(resetPeriod, x) = localMax(limit(1000,0,resetPeriod), x);

//----------------------------------------------------------------- TRIANGLE ---
triangularFunc(x) = abs(ma.frac((x - .5)) * 2.0 - 1.0);
triangleWave(f) = triangularFunc(os.phasor(1,f));

//------------------------------------------------------------------ FILTERS ---
onePoleTPT(cf, x) = loop ~ _ : ! , si.bus(3)
with {
    g = tan(cf * ma.PI * (1/ma.SR));
    G = g / (1.0 + g);
    loop(s) = u , lp , hp , ap
    with {
        v = (x - s) * G;
        u = v + lp;
        lp = v + s;
        hp = x - lp;
        ap = lp - hp;
    };
};
LPTPT(cf, x) = onePoleTPT(limit(20000,ma.EPSILON,cf), x) : (_ , ! , !);
HPTPT(cf, x) = onePoleTPT(limit(20000,ma.EPSILON,cf), x) : (! , _ , !);
// TEST
// process = (-100, no.noise) : HPTPT;

SVFTPT(K, Q, CF, x) = circuitout : ! , ! , _ , _ , _ , _ , _ , _ , _ , _
with{
    g = tan(CF * ma.PI / ma.SR);
    R = 1.0 / (2.0 * Q);
    G1 = 1.0 / (1.0 + 2.0 * R * g + g * g);
    G2 = 2.0 * R + g;
    circuit(s1, s2) = u1 , u2 , lp , hp , bp, notch, apf, ubp, peak, bshelf
        with{
            hp = (x - s1 * G2 - s2) * G1;
            v1 = hp * g;
            bp = s1 + v1;
            v2 = bp * g;
            lp = s2 + v2;
            u1 = v1 + bp;
            u2 = v2 + lp;
            notch = x - ((2*R)*bp);
            apf = x - ((4*R)*bp);
            ubp = ((2*R)*bp);
            peak = lp -hp;
            bshelf = x + (((2*K)*R)*bp);
        };
    // choose the output from the SVF Filter (ex. bshelf)
    circuitout = circuit ~ si.bus(2);
};
// Outs = (lp , hp , bp, notch, apf, ubp, peak, bshelf)
// SVFTPT(K, Q, CF, x) = (Filter-K, Filter-Q, Frequency Cut)

// Filters Bank
LPSVF(Q, CF, x) = SVFTPT(0, Q, 
    limit(20000,ma.EPSILON,CF), x) : _ , ! , ! , ! , ! , ! , ! , ! ;
HPSVF(Q, CF, x) = SVFTPT(0, Q, 
    limit(20000,ma.EPSILON,CF), x) : ! , _ , ! , ! , ! , ! , ! , !;
//process = (-1, -10000, no.noise) <: LPSVF, HPSVF;
BPsvftpt(BW, CF, x) = SVFTPT(0 : ba.db2linear, ql, cfl, 
    x ) : ! , ! , ! , ! , !, _ , ! , !
with{
    cfl = limit(20000,ma.EPSILON,CF);
    bwl = limit(20000,ma.EPSILON,BW);
    ql = cfl / bwl;
};
// TEST
//process = (1, 1000, no.noise) : BPsvftpt;

// Butterworth
butterworthQ(order, stage) = qFactor(order % 2)
with {
    qFactor(0) = 1.0 / (2.0 * cos(((2.0 * stage + 1) *
    (ma.PI / (order * 2.0)))));
    qFactor(1) = 1.0 / (2.0 * cos(((stage + 1) * (ma.PI / order))));
};

LPButterworthN(1, cf, x) = LPTPT(cf, x);
LPButterworthN(N, cf, x) = cascade(N % 2)
with {
    cascade(0) = x : seq(i, N / 2, LPSVF(butterworthQ(N, i), cf));
    cascade(1) = x : LPTPT(cf) : seq(i, (N - 1) / 2,
    LPSVF(butterworthQ(N, i), cf));
};

HPButterworthN(1, cf, x) = HPTPT(cf, x);
HPButterworthN(N, cf, x) = cascade(N % 2)
with {
    cascade(0) = x : seq(i, N / 2, HPSVF(butterworthQ(N, i), cf));
    cascade(1) = x : HPTPT(cf) : seq(i, (N - 1) /
    2, HPSVF(butterworthQ(N, i), cf));
};
//process = HPButterworthN(10, -1000, no.noise), 
 // LPButterworthN(10, -1000, no.noise);

// Order Aproximations filters - Outs
LP1(CF, x) = x : LPTPT(CF);
HP1(CF, x) = x : HPTPT(CF);
LP2(CF, x) = x : LPTPT(CF) : LPTPT(CF);
HP2(CF, x) = x : HPTPT(CF) : HPTPT(CF);
LP3(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
HP3(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);
LP4(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
HP4(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);
LP5(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
HP5(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);

//-------------------------------------------------------- GRANULAR SAMPLING ---
grain(L, position, duration, x, trigger) = hann(phase) * buffer(readPtr, x)
with {
    maxLength = 1920000;
    length = L * ma.SR;
    hann(ph) = sin(ma.PI * ph) ^ 2.0;
    lineSegment = loop ~ si.bus(2) : _ , ! , _
    with {
        loop(yState, incrementState) = y , increment , ready
        with {
            ready = ((yState == 0.0) | (yState == 1.0)) & trigger;
            y = ba.if(ready, increment, min(1.0, yState + increment));
            increment = ba.if(ready, ma.T / max(ma.T, duration), 
                incrementState);
        };
    };
    phase = lineSegment : _ , !;
    unlocking = lineSegment : ! , _;
    lock(param) = ba.sAndH(unlocking, param); 
    grainPosition = lock(position);
    grainDuration = lock(duration);
    readPtr = grainPosition * length + phase * grainDuration * ma.SR;
    buffer(readPtr, x) = 
        it.frwtable(3, maxLength, .0, writePtr, x, readPtrWrapped)
    with {
        writePtr = ba.period(length);
        readPtrWrapped = ma.modulo(readPtr, length);
    };
};

// works for N >= 2
triggerArray(N, rate) = loop ~ si.bus(3) : (! , ! , _) <: 
    par(i, N, == (i)) : par(i, N, \(x).(x > x'))
with {
    loop(incrState, phState, counterState) = incr , ph , counter
    with {
        init = 1 - 1';
        trigger = (phState < phState') + init;
        incr = ba.if(trigger, rate * ma.T, incrState);
        ph = ma.frac(incr + phState);
        counter = (trigger + counterState) % N;
    };
};

grainN(voices, L, position, rate, duration, x) = triggerArray(voices, rate) : 
    par(i, voices, grain(L, position, duration, x));

granular_sampling(var1, timeIndex, memWriteDel, cntrlLev, divDur, x) = 
    grainN(8, var1, position, rate, duration, x) :> /(8)
with {
    rnd = no.noise;
    memPointerJitter = rnd * (1.0 - memWriteDel) * .01;
    position = timeIndex * (1.0 - ((1.0 - memWriteDel) * .01)) + 
        memPointerJitter;
    density = 1.0 - cntrlLev;
    rate = 50 ^ (density * 2.0 - 1.0);
    grainDuration = .023 + (1.0 - memWriteDel) / divDur;
    duration = grainDuration + grainDuration * .1 * rnd;
};

//----------------------------------------------------- INPUTS/OUTPUTS MIXER ---
// input Mixer
GM1(x) = x * 
    si.smoo( ba.db2linear( vslider("GM1 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VM1 [unit:dB]", -80, 80));
GM2(x) = x * 
    si.smoo( ba.db2linear( vslider("GM2 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VM2 [unit:dB]", -80, 80));
GM3(x) = x *    
    si.smoo( ba.db2linear( vslider("GM3 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VM3 [unit:dB]", -80, 80));
GM4(x) = x * 
    si.smoo( ba.db2linear( vslider("GM4 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VM4 [unit:dB]", -80, 80));
// output Mixer
GO1(x) = x * 
    si.smoo(ba.db2linear( vslider("GO1 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO1 [unit:dB]", -80, 80));
GO2(x) = x * 
    si.smoo(ba.db2linear( vslider("GO2 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO2 [unit:dB]", -80, 80));
GO3(x) = x * 
    si.smoo(ba.db2linear( vslider("GO3 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO3 [unit:dB]", -80, 80));
GO4(x) = x * 
    si.smoo(ba.db2linear( vslider("GO4 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO4 [unit:dB]", -80, 80));
GO5(x) = x * 
    si.smoo(ba.db2linear( vslider("GO5 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO5 [unit:dB]", -80, 80));
GO6(x) = x * 
    si.smoo(ba.db2linear( vslider("GO6 [unit:db]", 0, -80, 80, .001) ) ) <:
        attach(_, VHmetersEnvelope(_) :vbargraph("VO6 [unit:dB]", -80, 80));

VHmetersEnvelope = abs : max ~ -(1.0/ma.SR) : 
    max(ba.db2linear(-70)) : ba.linear2db;

// inputs and outputs
Inputs_Mixer(m1, m2, m3, m4) = 
    hgroup("Inputs/Outputs Mixer", 
        ( 
            hgroup("MIC 1", GM1(m1)), 
            hgroup("MIC 2", GM2(m2)), 
            hgroup("MIC 3", GM3(m3)), 
            hgroup("MIC 4", GM4(m4))
        ) 
    );
    
Outputs_Mixer(o1, o2, o3, o4, o5, o6) = 
    hgroup("Inputs/Outputs Mixer", 
        ( 
            hgroup("OUT 1", GO1(o1)), 
            hgroup("OUT 2", GO2(o2)), 
            hgroup("OUT 3", GO3(o3)), 
            hgroup("OUT 4", GO4(o4)),
            hgroup("OUT 5", GO3(o5)), 
            hgroup("OUT 6", GO4(o6))
        ) 
    );
